plugins {
  id 'com.github.node-gradle.node' version '7.0.0'
}

node {
  download = false
  version = '18.17.1'
  npmVersion = ''
  workDir = file "${project.projectDir}/.gradle/nodejs"
  npmWorkDir = file "${project.projectDir}/.gradle/npm"
  nodeProxySettings = ProxySettings.SMART
}

File markupFile = file './mermaid.md'

task projectGraphMarkup {
  group 'documentation'
  description 'Generates markup file for project dependency graph'
  
  doLast{
    def links = []
    def projects = new TreeSet()
    project.subprojects.each{ module ->
      boolean isLibrary = module.plugins.hasPlugin 'java-library'
      String icon = isLibrary ? 'ðŸ“š' : module.name.contains( 'mothership' ) ? 'ðŸ›¸' : 'ðŸ—²'
      projects << """$module.name["$icon $module.name"]"""
        
      def extLinks = new TreeSet()
      [ 'api', 'implementation' ].each{ type -> 
        module.configurations.findByName( type )?.dependencies?.each{
          it.hasProperty( 'dependencyProject' ) ? links << "$module.name --> $it.name" : extLinks << it.name
        }
      }
      
      if( extLinks ){
        extLinks = 1 == extLinks.size() ? extLinks.first() : "+<br>${extLinks.join( '<br>' )}"
        projects << """$module.name-ext["ðŸ”— $extLinks"]:::ext"""
        links << "$module.name .-> $module.name-ext"
      }
    }
    
    markupFile.createNewFile()
    markupFile.withWriter( 'UTF-8' ){
      it << 'flowchart BT'
      it << '\n\tclassDef ext font-size:x-small, color:#888'
      it << '\n\t' << projects.join( '\n\t' )
      it << '\n\t' << links.join( '\n\t' )
    }
  }
}

task projectGraph( type:NpxTask ) {
  dependsOn projectGraphMarkup
  group 'documentation'
  description 'Generates project dependency graph'
  command = '@mermaid-js/mermaid-cli'
  args = [ '-i', 'mermaid.md', '-o', 'mermaid.svg' ]
}