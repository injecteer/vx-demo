plugins {
  id 'vx.demo.application-conventions'
  id 'vx.demo.node-conventions'
}

dependencies {
  implementation project( ':app' )
  implementation project( ':time-service' )
  implementation project( ':weather-service' )
  implementation project( ':vx-demo-backoffice' )
}

task verticles {
  group 'application'
  description 'Creates a list of verticle class names based on project dependencies'
  
  doLast{
    List projNames = configurations.implementation.dependencies.findResults{
      it.dependencyProject.plugins.hasPlugin( 'java-library' ) ? null : "$it.name-${it.version}.jar".toString()
    }
    List verticles = []
    configurations.compileClasspath.each{ dep ->
      if( dep.name !in projNames ) return
      String name, desc
      resources.text.fromArchiveEntry( dep, 'META-INF/MANIFEST.MF' ).asReader().splitEachLine( ': ' ){ parts ->
        switch( parts[ 0 ] ){
          case 'Main-Verticle': name = parts[ 1 ]; break
          case 'Description': desc = parts[ 1 ]; break
          case { desc && 1 == parts.size() }: desc += parts[ 0 ].trim(); break
        }
      }
      verticles << "$name >> $desc"
    }
    File f = file "$projectDir/src/main/resources/verticles.txt"
    f.createNewFile() 
    f.text = verticles.join( '\n' )
  }
}

jar.dependsOn verticles
devJar.dependsOn verticles

shadowJar.dependsOn processIndex